[SPELL 56]
DEFNAME=s_polymorph
NAME=Polymorph
SOUND=snd_spell_polymorph
RUNES=VYR
CAST_TIME=2.3
RESOURCES=i_reag_blood_moss,i_reag_mandrake_root,i_reag_spider_silk
RUNE_ITEM=i_rune_polymorph
SCROLL_ITEM=i_scroll_polymorph
FLAGS=spellflag_playeronly|spellflag_noprecast
EFFECT_ID=0
EFFECT=0
LAYER=layer_spell_polymorph
DURATION=5*60.0,8*60.0
MANAUSE=40
SKILLREQ=MAGERY 60.0
INTERRUPT=100.0,100.0

//Temporarily change your appearance with this spell. Your character's skills and stats are not effected, however you become grey while under the effects of this spell.
ON=@Select
if (<src.findid.i_rune_polymorph>)
  //findid.<serv.spell.s_polymorph.rune_item>.remove
  //effect 3,<serv.spell.s_dispel.effect_id>,6,15,1
  //sound <serv.spell.s_dispel.sound>
  src.sysmessage This spell is already in effect.
  return 1
elseif (<src.findid.i_rune_beastform>)
  src.sysmessage You cannot use polymorph while in beastform.
  return 1
elseif (<src.findid.i_rune_monster_form>)
  src.sysmessage You cannot use polymorph while in monster form.
  return 1
elseif (<src.findid.i_rune_gender_swap>)
  src.sysmessage You cannot use polymorph while gender swapped.
  return 1
elseif (<src.findid.i_rune_chameleon>)
  src.sysmessage You cannot use polymorph as a chameleon.
  return 1
elseif (<src.findid.i_tribal_paint_applied>)
  src.sysmessage You cannot use polymorph while wearing body paint.
  return 1
elseif (<src.findid.i_disguise_kit_incognito>)
  src.sysmessage You cannot use polymorph while disguised.
  return 1
elseif (<src.findid.i_barracoon_polymorphed_attacker>)
  src.sysmessage You cannot use polymorph right now.
  return 1
endif
height=12	//height should probably be 12, but you can't walk over some hills unless you are height 16
//this should turn you gray, handled in the notoriety event

on=@effect
ref1=<qval (!<argo>)?<src>:<argo>>
local.spell_strength=<ref1.f_spell_get_strength>
src.body=<src.actarg2>
src.f_polymorph_adjust_color	//make sure the color is correct

[function f_polymorph_adjust_color]
if (<ishuman>)
  color=colors_skin	//if we polyorph into a human give us a random skin color 
else
  color=0
endif



[EOF]