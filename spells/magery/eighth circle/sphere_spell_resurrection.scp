[SPELL 59]
DEFNAME=s_resurrection
NAME=Resurrection
SOUND=snd_spell_resurrection
RUNES=AC
CAST_TIME=2.5
RESOURCES=i_reag_blood_moss,i_reag_garlic,i_reag_ginseng
RUNE_ITEM=i_rune_resurrection
SCROLL_ITEM=i_scroll_resurrection
FLAGS=spellflag_dir_anim|spellflag_targ_char|spellflag_fx_targ|spellflag_good|spellflag_targ_dead|spellflag_playeronly|spellflag_scripted
EFFECT_ID=i_fx_heal_effect
EFFECT=0
DURATION=0.0
MANAUSE=50
SKILLREQ=MAGERY 70.0
INTERRUPT=100.0,100.0

//Enabled you to resurrect a player on a tile that does not contain another player or monster.

on=@start
if !(<src.isgm>) 
  local.distance=<src.distance <act>>
  if (<local.distance> > 1)
    src.sysmessage That is too far away.
    return 1
  elseif !(<act.f_resurrect_location_valid>)
    src.sysmessage Target cannot be resurrected at that location.
    return 1
  endif
endif

on=@success
if !(<src.isgm>) 
  local.distance=<src.distance <act>>
  if (<local.distance> > 1)
    src.sysmessage That is too far away.
    return 1
  elseif !(<act.f_resurrect_location_valid>)
    src.sysmessage Target cannot be resurrected at that location.
    return 1
  endif
endif

on=@effect
ref1=<qval (!<argo>)?<src>:<argo>>
local.spell_strength=<ref1.f_spell_get_strength>
if !(<src.isgm>) 
  local.distance=<src.distance <uid>>
  if (<local.distance> > 1)
    src.sysmessage That is too far away.
    return 1
  elseif !(<f_resurrect_location_valid>)
    src.sysmessage Target cannot be resurrected at that location.
    return 1
  endif
endif

if (<flags>&statf_dead)
  if (<npc>)
    resurrect 1000
  else
    flags |= statf_freeze
    trysrc <uid> dialogclose d_resurrect_confirm
    trysrc <uid> sdialog d_resurrect_confirm
    flags &= ~ statf_war
    flags |= statf_insubstantial 	//so npcs stop seeing them while they decide on resurrecting
    update
    effect=3,<serv.spell.s_resurrection.effect_id>,6,15,1
    sound 01f2
    return 0
  endif
endif

[function f_resurrect_location_valid]
ref1=<uid>
ref2=<region.uid> 
if (<ref2.isvalid>) && ((<ref2.type>==t_multi) || (<ref2.type>==t_multi_custom))
  if !(<ref2.f_house_isfriend <ref1>>)
    return 0
  endif
endif
forobjs 0
  if (<isitem>) && ((<type>==t_door) || (<type>==t_door_locked))
    return 0
  //elseif (<ischar>) && (<ref1> != <uid>)	//why wouldnt you be able to res on a char???
    //return 0
  endif
endfor
return 1

[dialog d_resurrect_confirm]
200,50
page 0
resizepic 10 10 2600 420 330
dtext 185 25 0 Resurrection
dhtmlgump 59 58 325 150 1 1 It is possible for you to be resurrected now. <def.br>Do you wish to try? <def.br>CONTINUE - You choose to try to come back to life now. <def.br>CANCEL - You prefer to remain a ghost for now.
button 85 235 4005 4007 1 0 1
dtext 125 240 0 CONTINUE
button 235 235 4005 4007 1 0 0
dtext 275 235 0 CANCEL

[dialog d_resurrect_confirm button]

onbutton=0
src.flags |= statf_war
src.flags &= ~ statf_insubstantial
src.flags &= ~ statf_freeze
serv.newitem i_resurrection_dialog_canceled
new.timer=6
src.equip <new>


onbutton=1
src.resurrect 1000
src.effect=3,<serv.spell.s_resurrection.effect_id>,6,15,1
src.flags &= ~ statf_freeze
src.sendpacket 072 00 00 32 00

[itemdef i_resurrection_dialog_canceled]	//mostly need to make sure healers dont spam players with resurrect dialogs
defname=i_resurrection_dialog_canceled
id=i_memory
name=Resurrection Dialog Canceled
type=t_eq_script
layer=layer_special

on=@timer
remove
return 1

[EOF]