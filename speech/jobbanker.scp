//****************************************************************************
//SPHERE by : Menasoft ©1997-2007
//www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: Friday, April 28, 2000
[SPEECH jobBANKER]
ON=*job*
ON=*work*
ON=*what*do*do*
ON=*profession*
ON=*occupation*
	IF ( <COMPLEXITY.HIGH> || <COMPLEXITY.MEDIUM> )
		DORAND 3
			SAY I am a Banker, <SRC.SEX milord/milady>.
			SAY I handle the money transactions for <REGION.NAME>, <SRC.SEX milord/milady>.
			SAY I am the local banker.
		ENDDO
	ELSE
		DORAND 3
			SAY I'm the Banker here, <SRC.SEX milord/milady>.
			SAY I handle the money for <REGION.NAME>, <SRC.SEX milord/milady>.
			SAY I'm the local banker.
		ENDDO
	ENDIF

ON=*Balance*
	SAY=You have <SRC.BANKBALANCE> gold in your account.

ON=*Bank*
	IF ( <COMPLEXITY.HIGH> || <COMPLEXITY.MEDIUM> )
		DORAND 3
			SAY Banking is a satisfactory experience.
			SAY We have no history of bank robberies at this branch.
			SAY My mother wanted me to adventure, but I find banking much more satisfying.
		ENDDO
	ELSE
		DORAND 3
			SAY Keeping the money of others is my job.
			SAY Never lost a coin from robberies.
			SAY Yes, banking is my unfortunate lot in life.
		ENDDO
	ENDIF
	src.BANKSELF
	src.message Bank container has <src.findlayer.29.fcount> items, <src.findlayer.29.weight> stones.

ON=hi
ON=hello*
ON=greetings*
ON=good*see*thee*
ON=Hail*
ON=Greet
	SAY=Good day, may I assist thee?

ON=*rob*
ON=*steal*
ON=*hold up*
	DORAND 4
		SAY Guards!  Guards they are robbing the bank!
		SAY You see, once a client's money.is deposited, no one can get to it. Not even me.
		SAY A thief could kill me and I still couldn't give any money away.
		SAY I can't give any money away unless a client specifically asks for a withdrawal.
	ENDDO

ON=*coin*
ON=*money*
ON=*currency*
ON=*deposit*
ON=*transaction*
ON=*account*
	IF ( <COMPLEXITY.HIGH> || <COMPLEXITY.MEDIUM> )
		DORAND 4
			SAY Thou can certainly leave thy money with us. 'Tis safer than in thine own pockets, I assure thee.
			SAY Thou art welcome to open an account at any time that we are open.
			SAY Just give thy money to me, <SRC.NAME>, and I shall take care of it for thee.
			SAY No one shall take thy gold as long as it is in our hands,<SRC.SEX milord/milady>.
		ENDDO
	ELSE
		DORAND 4
			SAY Thou can certainly leave thy money with us. 'Tis safer than in thine own pockets, I assure thee.
			SAY Thou can only open an account when we're open.
			SAY Just give thy money to me, <SRC.NAME>. 'Tis safe. I promise.
			SAY Dost thou not think me an honest man? No one will take thy gold as long as it's with us.
		ENDDO
	ENDIF

ON=*account*
	IF ( <COMPLEXITY.HIGH> )
		DORAND 2
			SAY If thou dost need to withdraw money from thy account, just say withdraw and the amount that thou dost need.
			SAY To find thy balance, ask for a statement or for thy balance.
		ENDDO
	ELIF ( <COMPLEXITY.MEDIUM> )
		DORAND 2
			SAY If thou dost need some of thy money, just ask to withdraw whatever amount thou needs.
			SAY To check how much money thou dost have in thy account, ask for a statement or for thy balance.
		ENDDO
	ELSE
		DORAND 2
			SAY To get some money out of thy account, ask to withdraw X. X bein' how much thou needs.
			SAY To check thy statement balance, ask for a statement or for thy balance. Easy as that.
		ENDDO
	ENDIF

ON=*gold*
ON=*silver*
ON=*copper*
	DORAND 3
		SAY Gold is the only metal we use when minting our coins.
		SAY Silver and copper just aren't suitable for coinage.
		SAY Gold is the basis for nearly all of our exchanges.
	ENDDO

ON=Bye
ON=Goodbye
ON=Farewell
	SAY=Goodbye.
	BYE
	return 1

ON=ACCOUNT
	SAY=I'm sorry, thine account information is private.

ON=COIN
	SAY=We accept only the coin of the realm.

ON=COPPER
	SAY=A copper piece is the smallest coin we accept.

ON=CURRENCY
	SAY=We deal in gold, silver and copper.

ON=Bank
ON=DEPOSIT
ON=GOLD
ON=MONEY
ON=SILVER
ON=TRANSACTION
	src.BANKSELF

ON=HOLD UP
	SAY=Guards! Guards!

on=check*
if (<src.flags>&statf_dead)
  return 1
endif
local.amount=<streat <args>>
if (!<isnum <local.amount>>)
  return 1
endif
argn=<local.amount>
if (<argn> < 5000)
  say We cannot create checks for such a paltry amount of gold!
  return 1
elseif (<argn> > <src.bankbalance>)
  say Ah, art thou trying to fool me? Thou has not so much gold!
  return 1
endif

//uid.<src.findlayer.29>.consume i_gold <argn>
src.findlayer.29.consumegold <argn>
serv.newitem i_bank_check,1,<src.findlayer.29>
new.more1=<argn>
src.sound 04f
src.findlayer.29.update
say Into your Bank Box I have placed a check in the amount of: <dargn>

on=withdrawl*
argn=<strarg <streat <args>>>
if (<argn> > <src.bankbalance>)
  say Ah, art thou trying to fool me? Thou has not so much gold!
  return 1
elseif (<argn> > <Serv.ItemsMaxAmount>)
  say Thou canst not widthdraw so much gold at one time!
  return 1
endif
trysrc <src.findlayer.29> consume <argn> i_gold 
serv.newitem i_gold,<argn>,<src.findlayer.21>
src.sound 037
say Thou hast withdrawn gold from they account.

ON=*buy*
ON=*supplies*
ON=*wares*
say Greetings. Have a look around
buy




[EOF]
