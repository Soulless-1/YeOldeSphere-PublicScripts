//OSI style moongate system by Soulless
//v1.0
//8:06 AM 8/31/2018

//updated to look more pleasing with the enhanced client



[defname moongate_settings]
moongates_facet0_active		1	//1=on 2=off
moongates_facet1_active		0
moongates_facet2_active		0
moongates_facet3_active		0
moongates_facet4_active		0
moongates_facet5_active		0

moongates_facet0	 Felucca
moongates_facet0_0 	4467,1283,5,0,Moonglow 		//moonglow
moongates_facet0_1 	1336,1997,5,0,Britain	 	//britain
moongates_facet0_2 	1499,3771,5,0,Jhelom		//jhelom
moongates_facet0_3 	771,752,5,0,Yew 			//yew
moongates_facet0_4 	3564,2140,34,0,Magincia 	//magincia
moongates_facet0_5 	2701,692,5,0,Minoc			//minoc
moongates_facet0_6 	1828,2948,-20,0,Trinsic 		//trinsic
moongates_facet0_7 	643,2067,5,0,Skara Brae		//skara brae
moongates_facet0_8 	2711,2234,0,0,Buccaneer's Den (Unguarded) 	//bucs den
moongates_facet0_8_hue	38
moongates_facet0_9	3725,2498,11,0,Ocllo (Newplayers)	//Ocllo
moongates_facet0_9_hue	63
moongates_facet0_10	

moongates_facet1	 Trammel
moongates_facet1_0 	4467,1283,5,1,Moonglow 		//moonglow
moongates_facet1_1 	1336,1997,5,1,Britain 		//britain
moongates_facet1_2 	1499,3771,5,1,Jhelom 		//jhelom
moongates_facet1_3 	771,752,5,1,Yew 			//yew
moongates_facet1_4 	3564,2140,34,1,Magincia 	//magincia
moongates_facet1_5 	2701,692,5,1,Minoc 			//minoc
moongates_facet1_6 	1828,2948,-20,1,Trinsic 		//trinsic
moongates_facet1_7 	643,2067,5,1,Skara Brae 		//skara brae
moongates_facet1_8 	3450,2677,26,1,New Haven 		//New Haven

moongates_facet2	 Ilshenar	
moongates_facet2_0 	1215,467,-13,2,Compassion 	//compassion	
moongates_facet2_1 	722,1366,-60,2,Honesty 	//honesty
moongates_facet2_2 	752,724,-27,2,Honor	 	//honor
moongates_facet2_3 	281,1016,0,2,Humility 	//humility
moongates_facet2_4 	987,1011,-32,2,Justice 	//justice
moongates_facet2_5 	1183,1287,-30,2,Sacrifice 	//sacrifice
moongates_facet2_6 	1525,1341,2,2,Spirituality 	//spirituality
moongates_facet2_7 	523,217,-46,2,Valor		//valor
moongates_facet2_8 	1749,236,58,2,Chaos		//chaos

moongates_facet3	 Malas
moongates_facet3_0 	1015,527,-65,3,Luna 	//luna
moongates_facet3_1 	1997,1386,-85,3,Umbra 	//umbra

moongates_facet4	 Tokuno Islands
moongates_facet4_0 	1169,998,42,4,Isamu-Jima 	//isamu jima
moongates_facet4_1 	802,1204,25,4,Makoto-Jima 	//makoto jima
moongates_facet4_2 	270,628,15,4,Homare-Jima 	//homare jima

moongates_facet5 	Ter Mur
moongates_facet5_0 	851,3525,-38,5,Royal City	//royal city

[itemdef i_moongate]
id=i_moongate_blue
type=t_script
name=Moongate

on=@DClick
if !(<src.gm>) 
  if (<src.distance> > 2)
    src.sysmessage You are too far away to use that.
    return 1
  //elseif (<src.flags>&statf_criminal) 
    //src.sysmessage @027 Thou'rt a criminal and cannot escape so easily...
    //return 1
  endif
endif

sdialog d_moongates <eval (<src.p.m>+1)>
sound snd_spell_gate_travel
return 1


on=@step 
if (<src.isplayer>)
  if (!<argn1>==1)   
    //if (<src.flags>&statf_criminal) && !(<src.isgm>)
      //src.sysmessage @027 Thou'rt a criminal and cannot escape so easily...
      //return 0
    //elseif (<src.recentpvp> < 300)
      //src.sysmessage @027 Wouldst thou flee during the heat of battle??
      //return 0
    //endif
    sdialog d_moongates <eval <src.p.m>+1>
    sound snd_spell_gate_travel
  endif 
endif

[dialog d_moongates]
90,90 

PAGE 0 
resizepic 10 10 9200 420 300 
//resizepic 20 20 3000 400 280

dtext 50 30 1263 Pick your destination: 

button 35 220 4005 4007 1 0 1000
dtext 70 220 1263 OKAY
button 35 245 4005 4007 1 0 0
dtext 70 245 1263 CANCEL

//Facet Listings
for 0 5
  if (<def0.moongates_facet<dlocal._for>_active>)
    button 30 <eval 62+(<local._for>*25)> 2117 2118 1 0 <eval <local._for>+1>
    dtext 50 <eval 60+(<local._for>*25)> 0 <def0.moongates_facet<dlocal._for>>
  endif
endfor

//Moongate Listings
//p = page
//f = facet
//m = moongate

page <argn1>

local.f=<eval <argn1>-1>
dtext 50 <eval 60+(<local.f>*25)> 044 <def0.moongates_facet<dlocal.f>>
while !<isempty <def.moongates_facet<dlocal.f>_<dlocal.n>>>
  //serv.b <eval (<dlocal.f>*100)+(<local.n>+1)>
  radio 210 <eval 42+(<local.y>*25)> 9026 9027 0 <eval (<dlocal.f>*100)+(<local.n>+1)>
  dtext 233 <eval 45+(<local.y>*25)> <qval (<def0.moongates_facet<dlocal.f>_<dlocal.n>_hue>)?<def0.moongates_facet<dlocal.f>_<dlocal.n>_hue>:0> <streat <streat <streat <streat <def0.moongates_facet<dlocal.f>_<dlocal.n>>>>>> 
  local.y += 1
  local.n += 1
endwhile

[dialog d_moongates button]

onbutton=1,6
if (<def0.moongates_facet<dargn1>_active> == 1)
  sdialog d_moongates <argn1>
endif

onbutton=1000
if (<distance> > 2)
  src.sysmessage You are too far away from the moongate.
  return 1
elseif !(<argchk>)
  src.sysmessage You did not select a location
  return 1
endif

//serv.b <eval <argchkid>-1>
local.n=<eval <argchkid>-1>
local.facet=<eval <local.n>/100>
local.moongate=<eval <local.n>-(<local.facet>*100)>
args=<def0.moongates_facet<dlocal.facet>_<dlocal.moongate>>
//serv.b <local.n> <local.facet> <local.moongate>
src.p=<argv[0]>,<argv[1]>,<argv[2]>,<argv[3]>
//src.flags |= statf_hidden
src.update
src.sound snd_spell_recall

[function place_moongates] 
forinstances i_moongate
  remove
endfor
for f 0 6
 if (<def0.moongates_facet<dlocal.f>_active>)
  for m 0 10
    if <def0.moongates_facet<dlocal.f>_<dlocal.m>>
      serv.newitem i_moongate
      new.p <def0.moongates_facet<dlocal.f>_<dlocal.m>>
      new.attr attr_static|attr_move_never 
    endif
  endfor
 endif
endfor
sysmessage Moongates placed

[function remove_moongates]
forinstances i_moongate
  remove
endfor
sysmessage Moongates removed

[EOF]