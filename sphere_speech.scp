//****************************************************************************
//SPHERE by : Menasoft ©1997-2007
//www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: 15-Nov-2003

VERSION=0.56b


// In the process of moving this stuff to scripts.

[SPEECH spk_player]
// All players speech filtered through here.

ON=*		//if anything is said
if (<tag0.flags_punishment>&<def0.flags_punishment_mutespeech_char>) || (<account.tag0.flags_punishment>&<def0.flags_punishment_mutespeech_account>)
  sysmessage You have been squelched from speaking.
  return 1
elseif (<account.plevel> > 1) && (<flags>&statf_insubstantial)
  sysmessage @026,0,1 Prevented from speaking while GM invisible.
  return 1
endif
f_writefile logs/player_speech<serv.rtime.format %Y-%m-%d>.log,<serv.rtime.format %H:%M>[Talking] <name>(<uid>): <args>
return 0

on=.*
if (<account.plevel> <= 1)
  sysmessage @026,0,1 Prevented from speaking invalid command.
  return 1
endif

on=*bank*
on=balance
on=check*
on=withdrawal*
if !(<flags>&statf_dead) && !(<flags>&statf_freeze) && !(<flags>&statf_stone)	//all the reasons they couldnt bank
  if (<def0.bank_<region.defname>_p>)	//is there a bank location for this area?
    local.MaxDistance=<def0.bank_<region.defname>_dist>
    local.Distance=<distance <def0.bank_<region.defname>_p>>
    if (<local.Distance> < <local.MaxDistance>) 
      for n 0 <eval (<serv.list.bank_<region.defname>_bankers.count>-1)>
        ref1=<serv.list.bank_<region.defname>_bankers.<dlocal.n>>
        local.distance=<distance <ref1>>		//we can can use this local again
        if (<local.distance> < <local.ClosestBanker>) || !(<local.ClosestBanker>)
          ref2=<serv.list.bank_<region.defname>_bankers.<dlocal.n>>
          local.ClosestBanker=<local.distance>
        endif
      endfor
      if (<ref2.isvalid>) 	//if we found a banker, use them
        ref2.hear <args>
        say @<speechcolor>,<font>,1 <args>
        return 1
      endif
    endif
  endif
endif

ON=I renounce my young player status
if (<tag0.young> >= 1)
  sdialog d_renounce_young_status
endif

ON=I must consider my sins
ref1=<findid.i_murder_short_term>
ref2=<findid.i_murder_long_term>
//if !(<ref1.isvalid>)		
  //sysmessage Fear not, thou hast not slain the innocent
//elseif (<ref1.amount> > <serv.MurderMinCount>) 
  //sysmessage If thou should return to the land of the living, the innocent shall wreak havoc upon thy soul.
//elseif (<ref1.amount> >= 1) 
  //sysmessage Although thou hast slain the innocent, thy deeds shall not bring retribution upon thy return to the living.
//elseif (<ref2.isvalid>
  //sysmessage Fear not, thou hast not slain the innocent in some time.
//endif
if (<ref1.isvalid>) && (<ref1.more1> > <serv.MurderMinCount>)
  sysmessage You are known throughout the land as a murderous brigand.
endif
sysmessage Short Term Murders: <qval (<ref1.isvalid>)?<ref1.dmore1>:0>
sysmessage Long Term Murders: <qval (<ref2.isvalid>)?<ref2.dmore1>:0>

ON=I resign from my guild	//this should be handled in the guild speech script?
ref1=<guild>
say @<speechcolor>,3,0,0 I resign from my guild
if (<ref1.isvalid>)	//they are in fact in a guild
  if (<ref1.masteruid>==<uid>)		//this is the guildmaster that resigned
    local.guildmaster_resigned=1
  endif
  ref1.resign <uid>
  src.sysmessage You have been dismissed from your guild.
  //f_guildsys_sendsmsg <ref1.uid>,<name> has resigned from the guild.
  if (<local.guildmaster_resigned>)
    if (<ref1.masteruid>)
      f_guildsys_sendsmsg <ref1.uid>,Guildmaster changed to <ref1.master>
    endif
  endif
  return 1
endif

/////////////////////////////////
// Faction Speech

on=I honor your leadership
if (<tag0.faction>)
  f_faction_point_transfer
endif

on=I resign from my faction
if (<guild>) && (<guild.masteruid> != <uid>)
  sysmessage You cannot leave this faction until your guildmaster has done so first.
  return 1
elseif (<findid.i_faction_leaving>)
  sysmessage You are already in the proecss of leaving your faction.
  return 1
endif
dialog d_faction_leave

on=What is my faction term status
if (<tag0.faction>) && (<findid.i_faction_leaving>)
  sysmessage Your term of service will come to an end in <eval ((<findid.i_faction_leaving.timer>/60)/60)> hours.
endif

on=i am sheriff
if (<tag0.faction>) 
  if (<var0.faction_control_<region.defname>> == <tag0.faction>) && (<var0.faction_<dtag0.faction>_<region.defname>_sheriff>==<uid>)
    ref1=<uid.<var0.faction_townstone_<region.defname>>>
    uid.<var0.faction_townstone_<region.defname>>.sdialog d_faction_town_stone_sheriff
  endif
endif

on=i wish to access the city treasury
if (<tag0.faction>) 
  if (<var0.faction_control_<region.defname>> == <tag0.faction>) && (<var0.faction_<dtag0.faction>_<region.defname>_finance_minister>==<uid>)
    ref1=<uid.<var0.faction_townstone_<region.defname>>>
    uid.<var0.faction_townstone_<region.defname>>.sdialog d_faction_town_stone_finance_minister
  endif
endif

on=punkte
if (<tag0.faction>) 
  say <dtag0.faction_points>
endif

on=message faction*
if ((<tag0.faction>) && (<var0.faction_<dtag0.faction>_leader> == <uid>)) || (<isgm>) 
  serv.allclients f_faction_commander_message <streat <streat <args>>>
endif


/////////////////////////////////
// Unlocking karma at shrines

on=Mu
if !(strcmpi(<region.name>,Compassion))
  tag0.karma_locked=
  sysmessage You once again control your destiny.
endif

on=Ahm
if !(strcmpi(<region.name>,Honesty))
  tag0.karma_locked=
  sysmessage You once again control your destiny.
endif

on=Sumn
if !(strcmpi(<region.name>,Honor))
  tag0.karma_locked=
  sysmessage You once again control your destiny.
endif

on=Lum
if !(strcmpi(<region.name>,Humility))
  tag0.karma_locked=
  sysmessage You once again control your destiny.
endif

on=Beh
if !(strcmpi(<region.name>,Justice))
  tag0.karma_locked=
  sysmessage You once again control your destiny.
endif

on=Cah
if !(strcmpi(<region.name>,Sacrifice))
  tag0.karma_locked=
  sysmessage You once again control your destiny.
endif

on=Om
if !(strcmpi(<region.name>,Spirituality))
  tag0.karma_locked=
  sysmessage You once again control your destiny.
endif

on=Ra
if !(strcmpi(<region.name>,Valor))
  tag0.karma_locked=
  sysmessage You once again control your destiny.
endif

on=Bal
if !(strcmpi(<region.name>,Chaos))
  tag0.karma_locked=1
  sysmessage You once again control your destiny.
endif

/////////////////////////
// T2A Passages
on=Recdu
if !(strcmpi(<p.x><p.y>,4545851))
  go 5736,3196,10
endif

on=Recsu
if !(strcmpi(<p.x><p.y>,57363196))
  go 4545,851,30
endif

on=Doracron 
//Activates the serpent pillars in Britannia's waters and transports you to the Lost Lands


//////////////////////////
// Guards

ON=Guard help *
ON=Guards*
ON=Help Guards*
ON=*Guards*
	// trying to call guards for help.

ON=I resign from my town

//////////////////////////

[SPEECH spk_pet]
// Pets that cannot talk

ON=DISMISS 
ON=RELEASE	
if (<owner>==<src>) || (<src.isgm>) 
  if (<isevent.e_player_vendor>)	//for some reason we can't skip past this here
    sdialog d_player_vendor_dismiss
  else
    sdialog d_pet_release
  endif
  return 1
endif

ON=TRANSFER
  if (<owner>==<src>) || (<src.isgm>)
    if (<isevent.e_player_vendor>)
      src.sysmessage You cannot transfer ownership of a player vendor.
      return 1
    elseif (<flags>&statf_dead)
      src.sysmessage You cannot transfer a pet that is dead.
      return 1
    endif
    if (<bonded>)
      message This pet is bonded, transfering it to another player will remove its bonded status.
    endif
    src.sysmessage Click on the person to transfer ownership to.
    targetf f_pet_trade <uid> 
    return 1
  endif

ON=COME
ON=FOLLOW
ON=FOLLOW ME
ON=DEFEND ME
  if (<owner>==<src>) || (<src.isgm>)
    if (<isevent.e_player_vendor>)
      return 1
    endif
  endif

ON=GUARD
ON=GUARD ME
  if (<owner>==<src>) || (<ispetsfriend <src>>) || (<src.isgm>)
    if (<isevent.e_player_vendor>)
      return 1
    endif
    local.controlchance=<src.f_pet_control_chance <uid>>
    if (<local.controlchance> < <R1,1000>) && !(<src.isgm>)
      return 1
    endif
    action=101
  endif    


ON=GO
ON=ATTACK
ON=KILL
  if (<owner>==<src>) || (<ispetsfriend <src>>) || (<src.isgm>)
    if (<isevent.e_player_vendor>)
      return 1
    endif
    local.controlchance=<src.f_pet_control_chance <uid>>
    if (<local.controlchance> < <R1,1000>) && !(<src.isgm>)
      return 1
    endif
  endif
  
ON=STAY
ON=STAY HERE
ON=STOP
  if (<owner>==<src>) || (<src.isgm>) || (<ispetsfriend <src>>)
    if (<isevent.e_player_vendor>)
      return 1
    endif
    //local.controlchance=<src.f_pet_control_chance <uid>>
    //if (<local.controlchance> < <R1,1000>) && !(<src.isgm>)
      //return 1
    //endif
    src.f_clear_bad_memories <uid>
    f_clear_all_bad_memories
    attacker.clear
    flags &=~ statf_war
    act=00
    action=101
  endif

ON=FRIEND
  if (<owner>==<src>) || (<src.isgm>) 
    if (<isevent.e_player_vendor>)
      return 1
    endif
    local.controlchance=<src.f_pet_control_chance <uid>>
    if (<local.controlchance> < <R1,1000>) && !(<src.isgm>)
      return 1
    endif
    src.sysmessage Click on the player whom you wish to make a co-owner.
    src.targetf f_pet_friend_targ <uid> 1
    return 1
  endif

ON=UNFRIEND
  if (<owner>==<src>) || (<src.isgm>) 
    if (<isevent.e_player_vendor>)
      return 1
    endif
    local.controlchance=<src.f_pet_control_chance <uid>>
    if (<local.controlchance> < <R1,1000>) && !(<src.isgm>)
      return 1
    endif
    src.sysmessage Click on the player whom you wish remove as a co-owner.
    src.targetf f_pet_friend_targ <uid> 0
    return 1
  endif

ON=SPEAK
  if (<owner>==<src>) || (<src.isgm>) 
    if (<isevent.e_player_vendor>)
      return 1
    endif
    local.controlchance=<src.f_pet_control_chance <uid>>
    if (<local.controlchance> < <R1,1000>) && !(<src.isgm>)
      return 1
    endif
    bark 1
  endif

ON=DROP	
ON=DROP ALL
//ON=FETCH
//ON="GIVE ?"
  if (<owner>==<src>) || (<src.isgm>) 
    if (<isevent.e_player_vendor>)
      return 1
    endif
    local.controlchance=<src.f_pet_control_chance <uid>>
    if (<local.controlchance> < <R1,1000>) && !(<src.isgm>)
      return 1
    endif
  endif


[SPEECH spk_hireling]
// Pets that can talk.
ON=ATTACK
ON=BOUGHT
ON=CASH
ON=COME
ON=DEFEND ME
ON=DISMISS
ON=DROP	// "GIVE" ?
ON=DROP ALL
ON=EQUIP
ON=EQUIP ALL
ON=FETCH
ON=FOLLOW
ON=FOLLOW ME
ON=FRIEND
ON=GET DRESSED
ON=GO
ON=GUARD
ON=GUARD ME
ON=INVENTORY
ON=KILL
ON=PRICE	// may have args ?
ON=RECIEVED
ON=RELEASE
ON=SAMPLES
ON=SPEAK
ON=STATUS
ON=STAY
ON=STAY HERE
ON=STOCK
ON=STOP
ON=SUIT UP
ON=TRANSFER

[EOF]
