//https://uo.stratics.com/content/skills/forensic.shtml
//http://www.uoguide.com/Forensic_Evaluation
//3:02 AM 3/27/2018

//todo add theives guild detect, trapped chest detect

[SKILL 19]
DEFNAME=Skill_Forensics
KEY=Forensics
TITLE=Detective
FLAGS=skf_scripted
PROMPT_MSG=Show me the crime.
DELAY=1.0
STAT_STR=10
STAT_INT=125
STAT_DEX=25
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=25
ADV_RATE=1.0,1.0,1.0    //2.5,50.0,200.0

on=@start
//act = target
//actprv = none

if !(<act.isvalid>) 
  return 1
elseif (<src.distance <act>> >= 8)
  src.sysmessage That is too far away.
  return 1
elseif !(<src.canseelos <act>>) && (<act.topobj> != <src>)
  src.sysmessage Target cannot be seen.
  return 1
endif
if !(<src.f_skillsucceed forensics>)
  src.actdiff=65535
else
  src.actdiff=0
endif
return 0

ON=@Fail
src.sysmessage You cannot determine anything useful.

ON=@Abort
src.sysmessage You decide you'd rather not look at that.

on=@success
if (<act.isitem>)

  if (<act.type>==t_corpse)
    if (<act.tag0.forensicist>)
      act.message The forensicist <uid.<act.tag0.forensicist>.name> has already discovered that:
    endif
    
    if (<act.link> != 04fffffff) && (<act.more2> != 0ffffffff)
      act.message This person was killed by <uid.<act.more2>.name>
    endif
  
    if !(<act.tag0.corpse_disturbed>)
      act.message This corpse has not been desecrated.
    else
      act.message This body has been disturbed by <act.f_corpse_disturbed>
    endif
    
    if !(<act.tag0.forensicist>)
      act.tag0.forensicist=<src>
    endif
  endif

  if (<act.type>==t_container>) || (<act.type>==t_container_locked)
    //Using Forensic Evaluation on a lockable chest will reveal who has recently picked the lock of that container.
    src.sysmessage You cannot determine anything useful.
  endif
endif

if (<act.ischar>)	//fixme
  //if is thieves guild member
    //Using Forensic Evaluation on a player will reveal their affiliation with the Thieves Guild.
    src.sysmessage You cannot determine anything useful.
  //endif
endif


on=@gain
if (<act.isitem>)
  if (<act.type> != t_corpse) && (<act.type> != t_container) && (<act.type> != t_container_locked)
    return 1
  endif
endif

[function f_corpse_disturbed]	//fixme: need to add this to the general events for players, also to chopping
if (strlen(<args>))
  ref1=<args>
  if (<ref1>)
    args=<tag0.corpse_disturbed>
    for 0 <eval (<argv>-1)>
      if !(strcmpi(<argv[<dlocal._for>]>,<ref1>))
        return 1
      endif
    endfor
    tag.corpse_disturbed .= <ref1>,
  endif
else
  args=<tag0.corpse_disturbed>
  for 0 <eval (<argv>-1)>
    local.names .= , <uid.<argv[<dlocal._for>]>.name>
  endfor
  return <streat <local.names>>
endif

[EOF]