//****************************************************************************
//SPHERE by : Menasoft ©1997-2007
//www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: 14-March-2007
VERSION=0.56b

[TYPEDEF T_STONE_GUILD]

ON=@Click
  message "a guildstone"
  message <name> <qval (<eval strlen(<abbrev>)>) ? [<abbrev>]:>
return 1

ON=@Dclick
  If (<distance> > 2) && !(<src.isgm>)
    src.sysmessage @038A That is too far away.
  elif (<member.count>==0) && (!<masteruid>)	//(<masteruid>!=<src.uid>)
    f_guildsys_addchartoguild <uid>,<src.uid>,1	//auto makes the master
  elif (!<masteruid>)
    //f_guildsys_addchartoguild <uid>,<member.0.uid>,1	//first candidate since any member would auto be a master
    masteruid = <member.0.uid>
    src.message @038b2 No guildmaster, promoting <uid.<masteruid>.name>.
  elif (<memberfromuid.<src.uid>.uid>!=<src.uid>) && (!<src.isgm>)// Not a member
    src.message @03B2 You are not a member of this guild! To join, ask a current member to invite you into the guild.
  elif (<memberfromuid.<src.uid>.priv> == <def0.STONEPRIV_ACCEPTED>)
    f_guildsys_addchartoguild <uid>,<src.uid>,1
  elif (<memberfromuid.<src.uid>.priv>==<def0.STONEPRIV_CANDIDATE>) && (!<src.isgm>)
    src.message @03B2 You are awaiting acceptance by the guildmaster.
  elif (<memberfromuid.<src.uid>.priv>==<def0.STONEPRIV_MEMBER>) || (<masteruid>==<src.uid>) || (<src.isgm>)
    sdialog d_Guild_Main,0
  endif
  ref1=<region.uid>
  if !(<ref1.isvalid>) || (<ref1.type> != t_multi)
    if (<timer> <= -1)
      timer=3600*6
    endif
    src.sysmessage @026 This guild stone is orphaned, it must be moved in <qval (<timer> <= 3600)?<eval (<timer>/60)> minutes:<eval ((<timer>/60)/60)> hours>.
  endif
return 1

on=@timer
  ref1=<region.uid>
  if !(<ref1.isvalid>) || (<ref1.type> != t_multi)
    serv.log <name>(<uid>) in <region.name>(<p>) was orphaned and as been removed.
    remove
  endif
return 1

on=@destroy		//removing the guildstone, let's remove the guildstone tag from the house it resided in.
ref1=<more2>	
if (<ref1.isvalid>)
  ref1.more2=		//clear guildstone from house
  serv.log <ref1.name>(<ref1>) house guildstone removed : <uid>
endif


[EOF]